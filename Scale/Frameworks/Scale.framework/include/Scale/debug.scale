import std.io
import std.threading.Thread

cdecl "print_stacktrace"
expect function trace(): none
cdecl "_scl_throw"
expect function throw(ex: Exception): none

cdecl "_scl_security_safe_exit"
expect function __c_exit(i: int): none
cdecl "_scl_catch_final"
expect function __c_scl_catch_final(_sig_: int): none
cdecl "raise"
expect function __c_raise(_sig_: int): int

function dumpStack(): none
  c!
    printf("Dump:\n");
    for (ssize_t i = _stack.ptr - 1; i >= 0; i--) {
      scl_int v = _stack.data[i].i;
      printf("   %zd: 0x" SCL_INT_HEX_FMT ", " SCL_INT_FMT "\n", i, v, v);
    }
	  printf("\n");
  end
end

function crash(): none
  1 __c_exit
end

function raise(_sig_: int): none
  if _sig_ 2 != _sig_ 4 != && _sig_ 6 != _sig_ 8 != && && _sig_ 11 != && then
    _sig_ __c_raise => decl raised
    if raised ! then
      _sig_ __c_scl_catch_final
    fi
  else
    _sig_ __c_scl_catch_final
  fi
end

cdecl "getchar"
expect function __c_getchar(): int8

function breakPoint(): none
  "Hit breakPoint. Press enter to continue." puts
  __c_getchar
end

sealed struct Exception
  decl msg: str
  decl stackTrace: const Array

  function init(): none
    sanitizedStackTrace => self.stackTrace
    "" => self.msg
  end

  private sealed function sanitizedStackTrace() stackTrace: Array
    Thread::stackTrace => stackTrace
    stackTrace:pop
    stackTrace:pop
    return
  end

  export sealed function printStackTrace(): none
    "Stacktrace of " typeof self + ":" + eputs
    for i in self.stackTrace.count 1 - to -1 step -- do
      "    " i self.stackTrace:get as str + eputs
    done
  end
end

sealed struct Error: Exception
  function init(msg: str): none
    sanitizedStackTrace => self.stackTrace
    msg => self.msg
  end
end

function sysPrettyString(): str
  40 malloc => decl sPtr: [int8]
  c!
    snprintf((*sPtr), 40, "%s %s", SCL_OS_NAME, SCL_SYSTEM);
  end
  sPtr str::new return
end
