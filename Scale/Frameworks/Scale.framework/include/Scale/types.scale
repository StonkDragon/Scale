typealias int32 "scl_int32"
typealias float32 "scl_float32"
typealias int16 "scl_int16"
typealias int8 "scl_int8"
typealias uint32 "scl_uint32"
typealias uint16 "scl_uint16"
typealias uint8 "scl_uint8"
typealias lambda "_scl_lambda"

sealed struct _String is IEquatable, ICloneable, IStringifyable
  function +(other: str): str
    other self:prepend return
  end

  function clone(): str
    self strdup return
  end

  function ++(c: int): str
    self:size 1 + malloc => decl newStr: mut str
    newStr self strcpy

    newStr self:size + => decl tmp: mut str
    c => @ tmp
    tmp 1 + => tmp
    0 => @ tmp

    newStr return
  end

  function append(other: str): str
    self other concat return
  end

  function prepend(other: str): str
    other self concat return
  end

  function equals(other: str): bool
    self other strcmp return
  end

  function ==(other: str): bool
    other self:equals return
  end

  function size(): int
    self strlen return
  end

  function at(i: int): int
    if i 0 < i self:size >= || then
      nameof i " is out of bounds":prepend eputs
      abort
    fi
    self i + @ as int toChar return
  end

  function toString(): str
    self as str return
  end

  function @(i: int): int
    i self:at return
  end
end
