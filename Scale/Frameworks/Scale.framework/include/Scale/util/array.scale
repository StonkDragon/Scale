import std.util.Iterable
import std.util.ImmutableArray
import std.debug
import std.util.InvalidArgumentException
import std.util.IndexOutOfBoundsException

sealed struct Array is IIterable
  decl values:  mut readonly [any]
  decl count:       readonly int
  decl capacity:    readonly int
  decl initCap:     readonly int

  private decl pos: readonly int
  
  function sort(): none
    0 => decl i

    while i self.count < do
      i self:get => decl tmp
      i 1 - => decl j
      while j 0 >= do
        if tmp j self:get < then
          j 1 + j self:get self:set
          j tmp self:set
        else
          j 1 + tmp self:set
          break
        fi
        j-- => j
      done
      i++ => i
    done
  end

  function get(index: int): any?
    if index 0 < index self.count >= || then
      "Index "
      index int::toString +
      " is out of bounds for Array of length " +
      self.count int::toString +
      "!" +
      IndexOutOfBoundsException::new throw
    fi
    self.values index sizeof any * + @ return
  end

  function set(index: int, value: any): none
    if index 0 < index self.capacity >= || then
      "Index "
      index int::toString +
      " is out of bounds for Array of capacity " +
      self.capacity int::toString +
      "!" +
      IndexOutOfBoundsException::new throw
    fi
    
    if index self.count >= then
      index 1 + => self.count
    fi

    value => @(self.values index sizeof any * +)
  end

  function top(): any?
    if self.count 0 <= then
      "Cannot invoke Array:top on an empty Array!" IndexOutOfBoundsException::new throw
    fi
    self.values self.count 1 - sizeof any * + @ return
  end

  function push(value: any): none
    if self.count self.capacity >= then
      self.capacity self.initCap + => self.capacity
      self.values self.capacity sizeof any * realloc
      => self.values
    fi

    value => @(self.values self.count sizeof any * +)
    self.count 1 + => self.count
  end

  function pop(): none
    if self.count 0 <= then
      "Cannot invoke Array:pop on an empty Array!" IndexOutOfBoundsException::new throw
    fi
    self.count 1 - => self.count
  end

  function contains(val: any): bool
    for i in 0 to self.count do
      if i self:get is SclObject then
        if val is SclObject then
          if
            i self:get as SclObject
            val as SclObject
            ==
          then
            true return
          fi
        fi
      else
        if i self:get val == then
          true return
        fi
      fi
    done
    false return
  end

  function init(size: int): none
    if size 1 < then
      "Invalid Argument: size must not be 0 or less" InvalidArgumentException::new throw
    fi
    size sizeof any * malloc
    => self.values
    0 => self.count
    0 => self.pos
    size => self.capacity
    size => self.initCap
  end

  function hasNext(): bool
    self.pos self.count < return
  end

  function next(): any?
    self.pos self:get
    self.pos 1 + => self.pos
    return
  end

  function reverse(): Array
    if self.count 0 <= then self return fi
    
    self.count Array::new => decl tmp
    foreach elem in self do
      elem tmp:push
    done
    0 => self.count
    for i in 0 to tmp.count do
      tmp:top self:push
      tmp:pop
    done
    tmp free
    self return
  end

  function toString() s: str
    if self.count 0 == then
      "{}" => s
    else
      "{" => s
      for i in 0 to self.count do
        i self:get => decl elem: any
        if i then
          s ", " + => s
        fi
        if elem is SclObject then
          s elem as SclObject :toString + => s
        else
          s elem as int int::toString + => s
        fi
      done
      s "}" + => s
    fi
    return
  end

  function immutableCopy(): ImmutableArray
    self.count sizeof any * malloc => decl newVals: [any]
    newVals self.values self.count sizeof any * memcpy => newVals
    self.count => decl newCount: int

    newCount newVals ImmutableArray::new return
  end

  function map(x: lambda(any): none): none
    foreach elem in self do
      elem x:accept
    done
  end

  function filter(x: lambda(any): bool) arr: Array
    self.capacity Array::new => arr
    foreach elem in self do
      if elem x:accept then
        elem arr:push
      fi
    done
    return
  end

  static function fromPointerCollection(count: int, values: [any]) arr: Array
    count Array::new => arr
    for i in 0 to count do
      values i sizeof any * + @ arr:push
    done
    return
  end
end
