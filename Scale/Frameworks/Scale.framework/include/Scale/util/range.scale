import std.util.Iterable

final sealed struct Range is IIterable
  decl lower: int
  decl upper: int
  
  private decl pos: int

  function init(theStart: int, theEnd: int): none
    if theStart theEnd > then
      "start must not be bigger than end!" InvalidArgumentException::new throw
    fi

    theStart => self.lower
    theEnd => self.upper
    0 => self.pos
  end

  function containsRange(other: Range): int
    if other.lower self:contains other.upper self:contains && then
      true return
    fi
    false return
  end

  function overlaps(other: Range): int
    if other.lower self:contains other.upper self:contains || then
      true return
    fi
    false return
  end

  function contains(pos: int): int
    if pos self.lower >= then
      if pos self.upper <= then
        true return
      fi
    fi
    false return
  end

  function toString(): str
    lower:toString " .. " + upper:toString + return
  end

  function begin(): none
    0 => self.pos
  end

  function hasNext(): bool
    self.pos self.lower + self.upper <= return
  end

  function next(): int
    self.lower self.pos +
    self.pos 1 + => self.pos
    return
  end

  function toReverseRange(): ReverseRange
    self.upper self.lower ReverseRange::new return
  end
end

final sealed struct ReverseRange is IIterable
  decl lower: int
  decl upper: int
  decl pos: int

  function init(theStart: int, theEnd: int): none
    if theStart theEnd < then
      "end must not be bigger than start!" InvalidArgumentException::new throw
    fi
    theStart => self.lower
    theEnd => self.upper
    0 => self.pos
  end

  function toString(): str
    lower:toString " .. " + upper:toString + return
  end

  function begin(): none
    0 => self.pos
  end
  
  function hasNext(): bool
    self.lower self.pos - self.upper >= return
  end

  function next(): int
    self.lower self.pos -
    self.pos 1 + => self.pos
    return
  end

  function toRange(): Range
    self.upper self.lower Range::new return
  end
end
