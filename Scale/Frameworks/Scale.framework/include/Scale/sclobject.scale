import Scale.util.cloneable
import Scale.util.stringifyable
import Scale.util.equatable
import Scale.util.hashable
import Scale.util.finalizable

struct SclObject is ICloneable, IStringifyable, IEquatable, IHashable, IFinalizable
    function toString(): str
        decl typename: mut str
        decl address: mut str
        inline_c
            *typename = (*self)->$__type_name__;
            *address = _scl_alloc(19);
            snprintf(*address, 18, "%p", *self);
        end_inline

        typename "@" + address + return
    end

    function ==(other: SclObject): bool
        self as any other as any == return
    end

    function equals(other: SclObject): bool
        self other == return
    end

    function clone() instance: SclObject
        decl sz: mut int
        inline_c
            *sz = (*self)->$__size__;
            *instance = _scl_alloc(*sz);
            _scl_add_struct(*instance);
        end_inline
        instance self sz memcpy
        return
    end

    function finalize(): none end

    function hashCode() hash: int
        inline_c
            *hash = (*self)->$__type__;
        end_inline
        return
    end

    function init(): none end
end
