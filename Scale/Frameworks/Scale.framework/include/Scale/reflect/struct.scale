final sealed struct Struct
    private decl typeName: str
    private decl typeId: int
    private decl superStruct: Struct?
    private decl binarySize: int
    private decl membersCount: int
    private decl members: [any]

    decl isStatic: readonly bool

    static decl structsCount: const int

    private function init(): none
        "Direct instantiation of struct 'Struct' is disabled!"
        IllegalStateException::new throw
    end

    function newInstance() inst: SclObject
        if self.binarySize 0 == then
            "Can not instantiate static struct '" self.typeName + "'" +
            IllegalStateException::new throw
        fi
        c!
            scl_int size = (*self)->binarySize;
            (*inst) = _scl_alloc(size);
            (*inst)->$__size__ = size;
            (*inst)->$__super__ = (*self)->superStruct ? (*self)->superStruct->typeId : 0;
            (*inst)->$__type__ = (*self)->typeId;
            (*inst)->$__type_name__ = (*self)->typeName->_data;
            _scl_add_struct(*inst);
        end
        inst:init
        return
    end

    function getMemberValue(instance: SclObject, name: str) val: any
        false => decl found: bool
        c!
            for (scl_int i = 0; i < (*self)->membersCount; i++) {
                struct _scl_membertype member = ((struct _scl_membertype*) (*self)->members)[i];
                if (member.pure_name == (*name)->_hash) {
                    *val = *(scl_any*) ((*(scl_int*) instance) + member.offset);
                    *found = 1;
                    break;
                }
            }
        end
        unless found then
            NullPointerException::new => decl e: mut Exception
            "Couldn't find variable '" name +
            "' on struct '" +
            self.typeName +
            "'" + => e.msg
            e throw
        fi
        return
    end

    function setMemberValue(instance: SclObject, name: str, val: any): none
        false => decl found: bool
        c!
            for (scl_int i = 0; i < (*self)->membersCount; i++) {
                struct _scl_membertype member = ((struct _scl_membertype*) (*self)->members)[i];
                if (member.pure_name == (*name)->_hash) {
                    *(scl_any*) ((*(scl_int*) instance) + member.offset) = *val;
                    *found = 1;
                    break;
                }
            }
        end
        unless found then
            NullPointerException::new => decl e: mut Exception
            "Couldn't find variable '" name +
            "' on struct '" +
            self.typeName +
            "'" + => e.msg
            e throw
        fi
    end

    function getMethodByName(name: str) method: lambda
        c!
            *method = _scl_get_method_handle((*self)->typeId, (*name)->_hash);
        end
        unless method then
            NullPointerException::new => decl e: mut Exception
            "Couldn't find method '" name +
            "' on struct '" +
            self.typeName +
            "'" + => e.msg
            e throw
        fi
        return
    end

    function getName(): str
        self.typeName return
    end

    function getSize(): int
        self.binarySize return
    end

    function getSuper(): Struct?
        self.superStruct return
    end

    static function getStructByName(name: str) theStruct: Struct
        c!
            *theStruct = _scl_get_struct_by_id((*name)->_hash);
        end
        unless theStruct then
            NullPointerException::new => decl e: mut Exception
            "Couldn't find struct '" name + "'" + => e.msg
            e throw
        fi
        return
    end
end
