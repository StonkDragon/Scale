import std.util.Array
import std.util.ReadOnlyArray

final struct InvalidTypeException : Exception
    function init(msg: str): none
        msg => self.msg
    end
end

final sealed struct TypedArray<T: SclObject>: Array
    overrides function [](index: int): T
        index self:get return
    end

    overrides function get(index: int): T
        if index 0 < index self.count >= || then
            "Index "
            index:toString +
            " is out of bounds for Array of length " +
            self.count:toString +
            "!" +
            IndexOutOfBoundsException::new throw
        fi
        self.values[index] as T return
    end

    overrides function =>[](index: int, value: T): none
        index value self:set
    end

    overrides function set(index: int, value: T): none
        if index 0 < index self.capacity >= || then
            "Index "
            index:toString +
            " is out of bounds for Array of capacity " +
            self.capacity:toString +
            "!" +
            IndexOutOfBoundsException::new
            throw
        fi
        
        if index self.count >= then
            index 1 + => self.count
        fi

        value => self.values[index]
    end

    overrides function top(): T
        if self.count 0 <= then
            "Cannot invoke Array:top on an empty Array!" IndexOutOfBoundsException::new throw
        fi
        self.values[self.count 1 -] as T return
    end

    overrides function push(value: T): none
        if self.count self.capacity >= then
            self.capacity self.initCap + => self.capacity
            self.capacity self.values:resize => self.values
        fi

        value => self.values[self.count]
        self.count 1 + => self.count
    end

    overrides function indexOf(val: T): int
        for i in 0 to self.count do
            if i self:get val == then
                i return
            fi
        done
        -1 return
    end

    overrides function contains(val: T): bool
        val self:indexOf -1 != return
    end

    overrides function remove(val: T): none
        val self:indexOf => decl index
        for i in index to self.count 1 - do
            i (i 1 + self:get) self:set
        done
        self.count 1 - => self.count
    end

    overrides function removeAll(val: T): none
        while val self:indexOf -1 != do
            val self:remove
        done
    end
end
