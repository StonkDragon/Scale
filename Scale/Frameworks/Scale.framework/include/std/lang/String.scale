import std.util.Stringifyable
import std.math

cdecl "strlen"
private expect foreign unsafe function __c_strlen(cstr: [int8]): int
cdecl "strcmp"
private expect foreign unsafe function __c_strcmp(firstCStr: [int8], secondCStr: [int8]): int
cdecl "s_strdup"
private expect foreign unsafe function __c_strdup(cstr: [int8]): [int8]
cdecl "strncmp"
private expect foreign unsafe function __c_strncmp(firstCStr: [int8], secondCStr: [int8], amount: int): int
cdecl "s_strcpy"
private expect foreign unsafe function __c_strcpy(dest: [int8], src: [int8]): [int8]
cdecl "strcat"
private expect foreign unsafe function __c_strcat(dest: [int8], src: [int8]): [int8]
cdecl "strtok"
private expect foreign unsafe function __c_strtok(:any, :[int8]): [int8]?

final sealed struct str is Equatable, Cloneable, Stringifyable, Iterable
  private decl data: [int8]
  private decl length: int
  private decl hash: uint

  function +(other: str): str
    other self:prepend return
  end

  function +(number: int): str
    number:toString self:prepend return
  end

  function +(number: float): str
    number:toString self:prepend return
  end

  function +(cStr: [int8]): str
    cStr str::new self:prepend return
  end

  function clone(): str
    self.data str::new return
  end

  function append(other: str): str
    other.data self:append return
  end

  function append(cStr: [int8]): str
    unsafe
      self.data cStr str::concat str::new return
    end
  end

  function prepend(other: str): str
    other.data self:prepend return
  end

  function prepend(cStr: [int8]): str
    unsafe
      cStr self.data str::concat str::new return
    end
  end

  function equals(other: any): bool
    if other is str ! then
      false return
    fi
    self.hash other as str.hash == return
  end

  function ==(other: any): bool
    other self:equals return
  end

  function !=(other: any): bool
    other self:equals ! return
  end

  function size(): int
    self.length return
  end

  function at(i: int): int
    if i 0 < i self:size >= || then
      "Index " i:toString +
      " is out of bounds for string with length " +
      self:size:toString +
      IndexOutOfBoundsException::new throw
    fi
    self.data[i] return
  end

  function indexOf(c: int8): int
    for i in 0 to self:size do
      if self[i] c == then
        i return
      fi
    done
    -1 return
  end

  function indexOf(s: str): int
    for i in 0 to self:size do
      if s (i self:subString):startsWith then
        i return
      fi
    done
    -1 return
  end

  function lastIndexOf(c: int8): int
    for i in self:size 1 - to -1 step -- do
      if self[i] c == then
        i return
      fi
    done
    -1 return
  end

  function contains(c: int8): bool
    c self:indexOf 0 > return
  end

  function toString(): str
    self return
  end

  function @(i: int): int
    i self:at return
  end

  function [](index: int): int8
    index self:at return
  end

  function reverse(): str
    self:size any::new as [int8] => decl out: [int8]
    0 => decl i: int
    unsafe
      for i in self.length 1 - to -1 step -- do
        self.length i - 1 - self @ => @(out as int i +)
      done
      '\0' => @(out as int self.length +)
    end
    out str::new
    out any::delete
    return
  end

  function toChars() _arr_: Array
    self:size Array::new => _arr_

    for _i_ in 0 to self:size do
      _i_ self:at _arr_:push
    done
    return
  end

  function trim(): str
    0 => decl startIndex: int
    self:size => decl endIndex: int
    try
      while self[startIndex] str::isSpace do
        startIndex 1 +> startIndex
      done

      while self[endIndex 1 -] str::isSpace do
        endIndex 1 -> endIndex
      done
    catch typeof IndexOutOfBoundsException
      "" return
    done

    if startIndex endIndex >= then
      "" return
    fi

    (startIndex to endIndex) self:subString return
  end

  function split(delim: str): [str]
    new<str>[self:size] => decl arr: [str]
    decl next: int

    self:view => decl tmp: str
    delim tmp:indexOf => decl index: int

    while index 0 >= do
      if index 0 == then
        "" => arr[next]
        next 1 +> next
      else
        (0 to index) tmp:subString => arr[next]
        next 1 +> next
      fi
      index delim:size + tmp:subString => tmp  
      delim tmp:indexOf => index
    done

    tmp => arr[next]
    next 1 +> next

    next arr:resize as [str] return
  end

  function startsWith(string: str): bool
    unsafe
      self.data string.data string:size __c_strncmp 0 == return
    end
  end

  function endsWith(string: str): bool
    if self.length string.length < then
      false return
    fi
    self.length string.length - self:subString => decl substr: str
    substr string ==
    
    substr any::delete

    return
  end

  function subString(start: int): str
    if start 0 == then
      self return
    fi
    if start self.length >= start 0 < || then
      "Index " start:toString +
      " is out of bounds for string with length " self:size:toString +
      +
      IndexOutOfBoundsException::new throw
    fi

    self:view => decl tmp: any

    tmp as int start + => tmp
    tmp as [int8] str::new return
  end

  function subString(range: Range): str
    if range.lower 0 < range.lower self:size >= || then
      "Index " range.lower:toString +
      " in range " +
      range:toString +
      " is out of bounds for string with length " self:size:toString +
      +
      IndexOutOfBoundsException::new throw
    fi
    if range.upper 0 < range.upper self:size > || then
      "Index " range.upper:toString +
      " in range " +
      range:toString +
      " is out of bounds for string with length " self:size:toString +
      +
      IndexOutOfBoundsException::new throw
    fi

    self:view => decl tmp: any
    tmp as int range.lower + => tmp
    tmp as [int8] => decl tmpArr: [int8]
    '\0' => tmpArr[range.upper range.lower -]
    tmpArr str::new return
  end

  function view(): [int8]
    unsafe
      self.data __c_strdup return
    end
  end

  function init(data: [int8]): none
    unsafe
      data __c_strdup => data
      data __c_strlen => self.length
    end
    data => self.data
    data builtinHash => self.hash
  end

  function init(data: [int8], n: int): none
    new<int8>[data:size n min 1 +] => self.data
    self.data:size => self.length
    for i in 0 to self.length do
      data[i] => self.data[i]
    done
    self.data builtinHash => self.hash
  end

  function init(): none
    c"" => self.data
    0 => self.length
    self.data builtinHash => self.hash
  end

  function iterate(): StringIterator
    self StringIterator::new return
  end

  function hashCode(): int
    self.hash return
  end

  function finalize(): none
    self.data any::delete
  end

  function utfLength(): int
    self do 'reduce lambda(acc: int, val: int8): int
      if val as uint8 0b11000000u8 & 0b10000000u8 == then
        acc return
      else
        acc++ return
      fi
    end return
  end

  static function format(fmt: str, args: varargs): str
    decl out: str
    
    0 => decl current: int
    args:size => decl size: int
    for i in 0 to fmt:size do
      fmt[i] => decl c: int8
      if c '%' == then
        i++ => i
        fmt[i] => c
        if c '%' == then
          out "%" + => out
          continue
        fi
        if current size >= then
          "Not enough arguments for format string" InvalidArgumentException::new throw
        fi
        args[current] => decl arg: any
        current++ => current
        if c 'i' == then
          out arg as int + => out
        elif c 's' == then
          out arg as str + => out
        elif c '@' == then
          if arg then
            out arg as SclObject:toString + => out
          else
            out "(nil)" + => out
          fi
        elif c 'f' == then
          out arg float::fromBits:toString + => out
        elif c 'p' == then
          out arg int::toHexString + => out
        elif c 'b' == then
          out arg bool::toString + => out
        elif c 'c' == then
          out arg int8::toString + => out
        else
          "Invalid format specifier at position #" i + InvalidArgumentException::new throw
        fi
      else
        out c:toString + => out
      fi
    done
    out return
  end

  static function =>(cStr: [int8]): str
    cStr str::new return
  end

  static function of(cStr: [int8]): str
    "" => decl s: str
    unsafe
      cStr __c_strlen => s.length
    end
    s.data any::delete
    cStr => s.data
    cStr builtinHash => s.hash
    s return
  end

  static function filter(arr: str, f: lambda(int8): bool): str
    new<int8>[arr:size] => decl result: [int8]
    0 => decl acc: int
    foreach char in arr do
      if char f:accept then
        char => result[acc]
        acc 1 +> acc
      fi
    done
    acc result:resize str::of return
  end

  static function map(arr: str, f: lambda(int8): int8): str
    new<int8>[arr:size] => decl result: [int8]
    for i in 0 to arr:size do
      arr[i] f:accept => result[i]
    done
    result str::of return
  end

  static reified function map<lambda(int8): U>(arr: str, f: lambda(int8): U): [U]
    new<U>[arr:size] => decl result: [U]
    for i in 0 to arr:size do
      arr[i] f:accept => result[i]
    done
    result return
  end

  static function reduce(arr: str, f: lambda(int, int8): int): int
    decl result: int
    for i in 0 to arr:size do
      result arr[i] f:accept => result
    done
    result return
  end

  static function count(arr: str, f: lambda(int8): bool): int
    decl acc: int
    foreach val in arr do
      if val f:accept then
        acc 1 +> acc
      fi
    done
    acc return
  end

  static function each(arr: str, f: lambda(int8): none): none
    foreach val in arr do
      val f:accept
    done
  end

  static function also(arr: str, f: lambda(int8): none): str
    foreach val in arr do
      val f:accept
    done
    arr return
  end

  static reified function transform<lambda(str): U>(arr: str, f: lambda(str): U): U
    arr f:accept return
  end

  private static function isSpace(c: int8): bool
    c ' ' == c '\t' == || c '\n' == c '\r' == || || c '\0' == || return
  end

  private static unsafe function concat(firstCStr: [int8], secondCStr: [int8]): [int8]
    firstCStr __c_strlen => decl len: int
    secondCStr __c_strlen => decl len2: int
    new<int8>[len len2 + 1 +] => decl out: [int8]

    out firstCStr __c_strcpy drop
    out secondCStr __c_strcat drop

    out return
  end
end

final sealed struct StringIterator is Iterator
  private decl _str: str
  private decl _pos: int

  function init(s: str): none
    s => self._str
    0 => self._pos
  end

  function next(): int8
    self._pos 1 + => self._pos
    self._str[self._pos 1 -] return
  end

  function hasNext(): bool
    self._pos self._str:size < return
  end
end

function +(s: str, i: int): str
  s i:toString + return
end

function +(s: str, f: float): str
  s f:toString + return
end
